
display_canvas = false
display_running is ubyte = false
display_width  = 800
display_height = 500

// Properties

terminal_fcolor_red is ubyte = 0u
terminal_fcolor_green is ubyte = 0u
terminal_fcolor_blue is ubyte = 0u
terminal_bcolor_red is ubyte = 0u
terminal_bcolor_green is ubyte = 0u
terminal_bcolor_blue is ubyte = 0u
terminal_cursor_x = 0u
terminal_cursor_y = 0u
terminal_font_map = false
terminal_font_map_path := "/Applications/Gismolang/TermFont.png"
terminal_font_w = 4u
terminal_font_h = 8u
terminal_msX = 0u
terminal_msY = 0u

MOUSEBUTTON_NONE   := 0
MOUSEBUTTON_LEFT   := 1
MOUSEBUTTON_MIDDLE := 2
MOUSEBUTTON_RIGHT  := 3

terminal_mouseMode = MOUSEBUTTON_NONE

terminal_buffer = ""

terminal_video_w = 0u
terminal_video_h = 0u
terminal_matrix_w = 0u
terminal_matrix_h = 0u
terminal_matrix := ""

terminal_scale = 0.5 // 0.2

terminal_in  = createChannel@os()
terminal_out = createChannel@os()

setupMatrix(ulong w, ulong h) {
    terminal_matrix = ""
    t = w * h
    i = 0u
    for i < t {
        terminal_matrix = terminal_matrix.InsertChar(#" '
        i = i + 1u
    }
}

updateSymAt(ulong c, ulong x, ulong y) {
    i = x + (y * terminal_matrix_w)
    if i < terminal_matrix.Length() {
        terminal_matrix.SetChar(i, c)
    }
}

selectSymAt(ulong x, ulong y) ulong {
    i = x + (y * terminal_matrix_w)
    if i < terminal_matrix.Length() {
        return terminal_matrix.CharAt(i)
    }
    return 0
}



vanguard term() {
    setup@SDL()
    display = createWindow@SDL()
    display.setTitle@SDL'GVOS Terminal'
    display.resizable@SDL(false)

    display.setSize@SDL(display_width, display_height)
    display.center@SDL()
    display.show@SDL()

    display_canvas = display.createRenderer@SDL()

    terminal_video_w  = display_width * terminal_scale
    terminal_video_h  = display_height * terminal_scale
    terminal_matrix_w = terminal_video_w / terminal_font_w
    terminal_matrix_h = terminal_video_h / terminal_font_h
    display_video     = display_canvas.emptyImage@SDL(terminal_video_w, terminal_video_h)
    terminal_font_map = display_canvas.loadImage@SDL(terminal_font_map_path)

    setupMatrix(terminal_matrix_w, terminal_matrix_h)
    
    // font settings

    setFont@Font(terminal_font_map)
    fontSettings@Font(
        128u,
        64u,
        terminal_font_w,
        terminal_font_h
    )

    currentSize@Font = 1.0
    fontFgColor@Font(
        terminal_fcolor_red,
        terminal_fcolor_green,
        terminal_fcolor_blue
    )
    fontBgColor@Font(
        terminal_bcolor_red,
        terminal_bcolor_green,
        terminal_bcolor_blue
    )

    display_canvas.drawTo@SDL(display_video)
    display_canvas.drawColor@SDL(0, 0, 0, 255)
    display_canvas.fillRect@SDL(0, 0, terminal_video_w, terminal_video_h)
    display_canvas.drawTo@SDL(0)

    display_running = true
    for display_running {
        // Event Poll
        display_running = handleEvents(display)

        // Channels Poll
        for terminal_in.listen@os() && display_running {
            display_running = handleTerm(display_canvas, display_video)
            display_running = handleEvents(display)
        }
        
        // Drawing
        display_canvas.drawColor@SDL(255, 255, 255, 255)
        display_canvas.drawImage@SDL(display_video, 0, 0, display_width, display_height)

        display_canvas.present@SDL()
        display_canvas.drawColor@SDL(0, 0, 0, 255)
        display_canvas.clearRenderer@SDL()

        delay@SDL(17)
    }

    terminal_font_map.destroyImage@SDL()
    display_video.destroyImage@SDL()
    display_canvas.destroyRenderer@SDL()
    display.destroyWindow@SDL()
    quit@SDL()
    exit(0)
}

handleEvents(Ref display) ubyte {
    event = true
    for event {
        event = display.checkEvent@SDL()
        if event == QUIT@SDL return false
        else if event == KEYDOWN@SDL vos_onKeyDown(readKeyDownEvent@SDL(), 3)
        else if event == KEYUP@SDL || event == MOUSEBUTTONUP@SDL vos_onKeyUp(readKeyUpEvent@SDL(), 3)
        // else if event == RENDER_TARGETS_RESET@SDL drawImages (renderer)
        else if event == MOUSEBUTTONDOWN@SDL vos_onKeyDown (0u, readMouseButtonDownEvent@SDL ())
        else if event == MOUSEBUTTONUP@SDL vos_onKeyUp (0u, readMouseButtonUpEvent@SDL ())
        else if event == MOUSEMOTION@SDL {
            terminal_msX = getMouseX@SDL()
            terminal_msY = getMouseY@SDL()
        }
        // else if event == MOUSEWHEEL@SDL vos_onMouseWheelMotion (readMouseWheelX@SDL (), readMouseWheelY@SDL ())
    }
    return true
}

KEYMODE_NORMAL  = 0
KEYMODE_SHIFT   = 1
KEYMODE_COMMAND = 2
KEYMODE_OPTION  = 3
KEYMODE_CONTROL = 4

keymode is ubyte = KEYMODE_NORMAL

vos_onKeyDown(Ref keycode, Ref mousebutton) {

    if mousebutton == 0u terminal_mouseMode = MOUSEBUTTON_LEFT
    else if mousebutton == 1u terminal_mouseMode = MOUSEBUTTON_MIDDLE
    else if mousebutton == 2u terminal_mouseMode = MOUSEBUTTON_RIGHT

    //output'keycode '", "".InsertChar(keycode), "' = ", keycode)
    if keycode == 1073742049 keymode = KEYMODE_SHIFT
    else if keycode == 1073742054 || keycode == 1073742050 keymode = KEYMODE_OPTION
    else if keycode == 1073742048 keymode = KEYMODE_CONTROL
    else if keycode == 1073742051 keymode = KEYMODE_COMMAND
    
    if keymode == KEYMODE_OPTION {
        if      keycode == "5".CharAt(0) terminal_buffer = terminal_buffer.InsertChar (#"['
        else if keycode == "6".CharAt(0) terminal_buffer = terminal_buffer.InsertChar (#"]'
        else if keycode == "8".CharAt(0) terminal_buffer = terminal_buffer.InsertChar (#"{'
        else if keycode == "9".CharAt(0) terminal_buffer = terminal_buffer.InsertChar (#"}'
        else if keycode == "7".CharAt(0) terminal_buffer = terminal_buffer.InsertChar (#"|'
        else if keycode == "l".CharAt(0) terminal_buffer = terminal_buffer.InsertChar (#"@'
        else if keycode == "n".CharAt(0) terminal_buffer = terminal_buffer.InsertChar (#"~'
        else if keycode == 223 terminal_buffer = terminal_buffer.InsertChar (#"\\'
    } else if keymode == KEYMODE_SHIFT 
        if keycode >= "a".CharAt (0) && keycode <= "z".CharAt (0)
            terminal_buffer = terminal_buffer.InsertChar (keycode - 32)
        else if keycode == "4".CharAt (0)
            terminal_buffer = terminal_buffer.InsertChar ('$'))
        else if keycode == "8".CharAt (0)
            terminal_buffer = terminal_buffer.InsertChar ('(')
        else if keycode == "9".CharAt (0)
            terminal_buffer = terminal_buffer.InsertChar (')')
        else if keycode == "+".CharAt (0)
            terminal_buffer = terminal_buffer.InsertChar ('*')
        else if keycode == "-".CharAt (0)
            terminal_buffer = terminal_buffer.InsertChar ('_')
        else if keycode == "7".CharAt (0)
            terminal_buffer = terminal_buffer.InsertChar ('/')
        else if keycode == "\t".CharAt (0)
            terminal_buffer = terminal_buffer + "\t"
        else if keycode == 94
            terminal_buffer = terminal_buffer.InsertChar ('>')
        else if keycode == "0".CharAt (0)
            terminal_buffer = terminal_buffer.InsertChar ('=')
        else if keycode == "1".CharAt (0)
            terminal_buffer = terminal_buffer.InsertChar ('!')
        else if keycode == "2".CharAt (0)
            terminal_buffer = terminal_buffer.InsertChar ('\"')
        else if keycode == "5".CharAt (0)
            terminal_buffer = terminal_buffer.InsertChar ('%')
        else if keycode == "6".CharAt (0)
            terminal_buffer = terminal_buffer.InsertChar ('&')
        else if keycode == ".".CharAt (0)
            terminal_buffer = terminal_buffer.InsertChar (':')
        else if keycode == ",".CharAt (0)
            terminal_buffer = terminal_buffer.InsertChar (';')
        else if keycode == 223
            terminal_buffer = terminal_buffer.InsertChar ('?')
        else if keycode == "#".CharAt(0)
            terminal_buffer = terminal_buffer.InsertChar ('\'')
        else if keycode == " ".CharAt(0) { 
            terminal_buffer = terminal_buffer.InsertChar (keycode)
        } else if keycode == 8 {
            terminal_buffer = terminal_buffer.InsertChar (8)
            //if TerminalCursorPos != 0u && TerminalCursorPos < (TerminalContent.Length() + 1) {
            //    TerminalContent = TerminalContent.Remove (TerminalCursorPos - 1u)
            //    TerminalCursorPos = TerminalCursorPos - 1u
            //}
        } else if keycode == 13 {			
            terminal_buffer = terminal_buffer.InsertChar (#"\n'
        }
    else if keycode >= "a".CharAt (0) && keycode <= "z".CharAt (0) {
        terminal_buffer = terminal_buffer.InsertChar (keycode)
    } else if keycode >= 32 && keycode <= 64 {
        terminal_buffer = terminal_buffer.InsertChar (keycode)
    } else if keycode == " ".CharAt(0) { 
        terminal_buffer = terminal_buffer.InsertChar (keycode)
    } else if keycode == "\t".CharAt (0)
        terminal_buffer = terminal_buffer + "\t"
    else if keycode == 8 {
        terminal_buffer = terminal_buffer.InsertChar (8)
        //if TerminalCursorPos != 0u && TerminalCursorPos < (TerminalContent.Length() + 1) {
        //    TerminalContent = TerminalContent.Remove (TerminalCursorPos - 1u)
        //    TerminalCursorPos = TerminalCursorPos - 1u
        //}
    } else if keycode == 13 {			
        terminal_buffer = terminal_buffer.InsertChar (#"\n'
    } else if keycode == 60 {
        terminal_buffer = terminal_buffer.InsertChar ('^')
    } else if keycode == 94 {
        terminal_buffer = terminal_buffer.InsertChar ('<')
    } else if keycode == 1073741906 {
        terminal_buffer = terminal_buffer.InsertChar (24)
    } else if keycode == 1073741905 {
        terminal_buffer = terminal_buffer.InsertChar (25)
    } else if keycode == 1073741904 {
        terminal_buffer = terminal_buffer.InsertChar (27)
    } else if keycode == 1073741903 {
        terminal_buffer = terminal_buffer.InsertChar (26)
    }
}

vos_onKeyUp(Ref keycode, Ref mousebutton) {
    if mousebutton == 0u terminal_mouseMode = MOUSEBUTTON_NONE
    else if mousebutton == 1u terminal_mouseMode = MOUSEBUTTON_NONE
    else if mousebutton == 2u terminal_mouseMode = MOUSEBUTTON_NONE
    else if mousebutton == 3u terminal_mouseMode = MOUSEBUTTON_NONE

    if keycode == 1073742049 keymode = KEYMODE_NORMAL
    else if keycode == 1073742054 || keycode == 1073742050 keymode = KEYMODE_NORMAL
    else if keycode == 1073742048 keymode = KEYMODE_NORMAL
    else if keycode == 1073742051 keymode = KEYMODE_NORMAL
}

// Communications

TERM_CODEX_DONE         = 0u
TERM_CODEX_MVCURSOR     = 1u
TERM_CODEX_DWSYM        = 2u
TERM_CODEX_DWFGCOLOR    = 3u
TERM_CODEX_DWBGCOLOR    = 4u
TERM_CODEX_USEMAP       = 5u
TERM_CODEX_CLS          = 6u
TERM_CODEX_GCHAR        = 7u
TERM_CODEX_GMOUSEPOS    = 8u

handleTerm(Ref canvas, Ref video) ubyte {

    codex = terminal_in.receiveUInt@os()

    if codex == TERM_CODEX_DONE {
        return false
    } else if codex == TERM_CODEX_MVCURSOR {
        terminal_cursor_x = terminal_in.receiveUInt@os()
        terminal_cursor_y = terminal_in.receiveUInt@os()
    } else if codex == TERM_CODEX_DWSYM {
        symbol = terminal_in.receiveUInt@os()
        updateSymAt(symbol, terminal_cursor_x, terminal_cursor_y)
        canvas.drawTo@SDL(video)
        canvas.drawCharacterColoredMatrix@Font(terminal_cursor_x + (terminal_cursor_y * terminal_matrix_w), terminal_matrix_w, symbol)
        canvas.drawTo@SDL(0)
    } else if codex == TERM_CODEX_DWFGCOLOR {
        terminal_fcolor_red = terminal_in.receiveUInt@os()
        terminal_fcolor_green = terminal_in.receiveUInt@os()
        terminal_fcolor_blue = terminal_in.receiveUInt@os()

        fontFgColor@Font(
            terminal_fcolor_red,
            terminal_fcolor_green,
            terminal_fcolor_blue
        )
    } else if codex == TERM_CODEX_DWBGCOLOR {
        terminal_bcolor_red = terminal_in.receiveUInt@os()
        terminal_bcolor_green = terminal_in.receiveUInt@os()
        terminal_bcolor_blue = terminal_in.receiveUInt@os()

        fontBgColor@Font(
            terminal_bcolor_red,
            terminal_bcolor_green,
            terminal_bcolor_blue
        )
    } else if codex == TERM_CODEX_USEMAP {
        terminal_font_map.destroyImage@SDL()
        terminal_font_map = terminal_in.receiveUInt@os()
    } else if codex == TERM_CODEX_CLS {
        canvas.drawTo@SDL(video)
        canvas.drawColor@SDL(terminal_bcolor_red, terminal_bcolor_green, terminal_bcolor_blue, 255)
        canvas.fillRect@SDL(0, 0, terminal_video_w, terminal_video_h)
        canvas.drawTo@SDL(0)
    } else if codex == TERM_CODEX_GCHAR {
        if !terminal_buffer.Length() {
            terminal_out.sendUInt@os(0u)
        } else {
            terminal_out.sendUInt@os(terminal_buffer.CharAt(0u))
            terminal_buffer = terminal_buffer.Remove(0u)
        }
    } else if codex == TERM_CODEX_GMOUSEPOS {
        tile_x is long = ((terminal_msX*terminal_scale) as long) / (terminal_font_w as long)
        tile_y is long = ((terminal_msY*terminal_scale) as long) / (terminal_font_h as long)
        terminal_out.sendInt@os(tile_x)
        terminal_out.sendInt@os(tile_y)
    }

    return true
}

// Input Output Functions

dnTerm() {
    terminal_in.sendUInt@os(TERM_CODEX_DONE)
}

mvCursor(ulong x, ulong y) {
    terminal_in.sendUInt@os(TERM_CODEX_MVCURSOR)
    terminal_in.sendUInt@os(x)
    terminal_in.sendUInt@os(y)
}

dwSymbol(ubyte symbol) {
    terminal_in.sendUInt@os(TERM_CODEX_DWSYM)
    terminal_in.sendUInt@os(symbol)
}

dwfgColor(ulong r, ulong g, ulong b) {
    terminal_in.sendUInt@os(TERM_CODEX_DWFGCOLOR)
    terminal_in.sendUInt@os(r)
    terminal_in.sendUInt@os(g)
    terminal_in.sendUInt@os(b)
}

dwbgColor(ulong r, ulong g, ulong b) {
    terminal_in.sendUInt@os(TERM_CODEX_DWBGCOLOR)
    terminal_in.sendUInt@os(r)
    terminal_in.sendUInt@os(g)
    terminal_in.sendUInt@os(b)
}

useMap(Ref map) {
    terminal_in.sendUInt@os(TERM_CODEX_USEMAP)
    terminal_in.sendUInt@os(map)
}

clrDisp() {
    terminal_in.sendUInt@os(TERM_CODEX_CLS)
}

gchar() ubyte {
    ans is ubyte = 0u
    for !ans {
        terminal_in.sendUInt@os(TERM_CODEX_GCHAR)
        ans = terminal_out.receiveUInt@os()
        // wait@os(15)
    }
    return ans
}

cgchar() ubyte {
    // wait@os(15)
    terminal_in.sendUInt@os(TERM_CODEX_GCHAR)
    return terminal_out.receiveUInt@os()
}

getMouseChar() txt {
    terminal_in.sendUInt@os(TERM_CODEX_GMOUSEPOS)
    x = terminal_out.receiveInt@os()
    y = terminal_out.receiveInt@os()
    if x < 0 x = 0
    if y < 0 y = 0
    return tuple@std(x as ulong, y as ulong)
}